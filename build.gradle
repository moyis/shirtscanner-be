plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'

    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.5'

    id "org.jlleitschuh.gradle.ktlint" version '12.1.2'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'dev.moyis'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //Jackson
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    //Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    //Logging
    implementation 'io.github.microutils:kotlin-logging:3.0.5'

    //Cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'

    //Scraping
    implementation 'org.jsoup:jsoup:1.18.3'

    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    testImplementation 'io.rest-assured:kotlin-extensions:5.5.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'

    testImplementation 'org.wiremock:wiremock-standalone:3.12.0'
    testImplementation 'org.wiremock.integrations.testcontainers:wiremock-testcontainers-module:1.0-alpha-14'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named("bootBuildImage") {
    environment = [
            'BP_JVM_VERSION'                : '21',
            'BP_JVM_CDS_ENABLED'            : 'true',
            'CDS_TRAINING_JAVA_TOOL_OPTIONS': '-Dspring.flyway.enabled=false',
    ]
}

pitest {
    pitestVersion = "1.17.2"
    junit5PluginVersion = "1.2.1"
    threads = 4
    targetClasses = [
            'dev.moyis.shirtscanner.domain.*',
            'dev.moyis.shirtscanner.infrastructure.controllers.*',
            'dev.moyis.shirtscanner.infrastructure.services.*',
            'dev.moyis.shirtscanner.infrastructure.repositories.*',
    ]
}